#
# configure.in
# 
# This file is part of the Oxford Oberon-2 compiler
# Copyright (c) 2006--2016 J. M. Spivey
# All rights reserved
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met: 
#
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above
#    copyright notice, this list of conditions and the following
#    disclaimer in the documentation and/or other materials provided
#    with the distribution. 
# 3. The name of the author may not be used to endorse or promote products
#    derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS
# OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
# GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
# NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#

# Process this file with autoconf to produce a configure script.

AC_INIT(obc, 0.1, mike@cs.ox.ac.uk)
AC_PREREQ(2.50)
AC_CONFIG_SRCDIR(vm.h)
AC_CONFIG_FILES(Makefile)
AC_CONFIG_HEADERS(config.h)
AC_CANONICAL_HOST

: ${AUTOCONF=autoconf} ${AUTOHEADER=autoheader}
AC_SUBST(AUTOCONF)
AC_SUBST(AUTOHEADER)

# Find out if this is Windows or MacOS
# Properly speaking, on Cygwin we are doing a cross-build.
case $host in
*-*-linux*)
  AC_DEFINE(LINUX, 1, [Define for Linux]);;
*-*-mingw|*-*-cygwin) 
  AC_SUBST(WINDOWS, 1)
  AC_DEFINE(WINDOWS, 1, [Define to enable hacks for Windows])
  AC_SUBST(ROOT, C:/cygwin)
  AC_SUBST(CFLAGS_JIT, [-Wl,--stack,4194304]);;
*-*-darwin*)
  AC_SUBST(MACOS, 1)
  AC_DEFINE(MACOS, 1, [Define to enable hacks for MacOS X]);;
esac

# Checks for programs.
case $host in x86_64-*) CC='gcc -m32';; esac

AC_PROG_CC
AC_PROG_RANLIB

# Disable fancy alias analysis if compiling with gcc
: ${RTFLAGS='${CFLAGS}'}
if test "$GCC" == yes; then
    RTFLAGS="$RTFLAGS -fno-strict-aliasing"
fi
AC_SUBST(RTFLAGS)

# Package options
AC_ARG_ENABLE(debug,
  AS_HELP_STRING([--enable-debug], [set up for debugging]),
  if test "$enable_debug" = yes; then 
    DEBUG=1
    AC_DEFINE(DEBUG, 1, [Define to set up for debugging]) 
  fi)
AC_SUBST(DEBUG)

case $host in
    i?86-*|x86_64-*) 
        VM=vm386.o;;
    armv6l-*|armv7l-*|arm-*)
        VM=vmpi.o
        AC_DEFINE(USE_FLUSH, 1, [Whether to flush the cache]);;
esac

case $host in
    *-*-darwin*|*-*-linux*) 
        AC_DEFINE(USE_MPROTECT, 1, [Whether to call mprotect from JIT]);;
esac		

AC_SUBST(VM)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_FIND_PAGESIZE

# Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(mmap sigprocmask) # assume they work OK for our purposes

# The endgame
AC_OUTPUT
